"$_flox_activate_tracer" "$_activate_d/zsh" START

_sed="@gnused@/bin/sed"
_flox_activations="@flox_activations@"

# Confirm _flox_activate_tracelevel is defined before proceeding.
if [ -z "${_flox_activate_tracelevel:=}" ]; then
  echo 'WARNING (zsh): _flox_activate_tracelevel not defined .. defaulting to 0' >&2
  export _flox_activate_tracelevel=0
fi

# Enable shell-specific profile script startup with verbosity 2.
if [ "$_flox_activate_tracelevel" -ge 2 ]; then
  set -x
fi

# This is the final script to be called in the zsh startup sequence so start
# by restoring the original value of ZDOTDIR if it was set previously.
if [ -n "${FLOX_ORIG_ZDOTDIR:-}" ]
then
  export ZDOTDIR="$FLOX_ORIG_ZDOTDIR"
  unset FLOX_ORIG_ZDOTDIR
else
  unset ZDOTDIR
fi

# Modify dynamic zsh fpath in preference to FPATH in environment.
# See https://github.com/flox/flox/pull/1299 for more details.
declare -a fpath_prepend=()
for i in "${(@s/:/)FLOX_ENV_DIRS}"; do
  # Check if fpath already contains this env's site-functions directory.
  # The trick here is that the "i" means inverse subscript, meaning that
  # it returns the index of the value which follows, and "e" specifies an
  # exact match. If the value is found it returns the index of the matching
  # element, but if not it returns the length of the array + 1, hence we
  # check for any value greater than the length of the array.
  # TODO: apple puts their stuff first so re-sort fpath by putting flox envs
  #       first by paring the latter appearances from fpath.
  if [[ -n "$i" && $fpath[(ie)$i/share/zsh/site-functions] -gt ${#fpath} ]]; then
    fpath_prepend+=( "$i"/share/zsh/site-functions "$i"/share/zsh/vendor-completions )
  fi
done
if [ ${#fpath_prepend[@]} -gt 0 ]; then
  fpath=( "${fpath_prepend[@]}" "${fpath[@]}" )
  autoload -U compinit

  # compinit tracing is very verbose so only enable with tracelevel 3 or above.
  if [ "$_flox_activate_tracelevel" -eq 2 ]; then
    # Disable tracing for the compinit invocation then re-enable.
    : compinit tracing suppressed
    set +x
    compinit
    set -x
  else
    # Tracing will either have already been enabled for tracelevels greater or
    # equal to 2 or not enabled for tracelevels below 2, but either way we can
    # leave tracing as-is and just invoke compinit.
    compinit
  fi

fi
unset fpath_prepend

if [ "${_FLOX_ACTIVATION_PROFILE_ONLY:-}" != true ]; then
  # Restore environment variables set in the previous bash initialization.
  eval "$($_sed -e 's/^/unset /' -e 's/$/;/' "$_FLOX_ACTIVATION_STATE_DIR/del.env")"
  eval "$($_sed -e 's/^/export /' -e 's/$/;/' "$_FLOX_ACTIVATION_STATE_DIR/add.env")"
fi

# Set the prompt if we're in an interactive shell.
if [[ -o interactive ]]; then
  source "$_activate_d/set-prompt.zsh"
fi

# We already customized the PATH and MANPATH, but the user and system
# dotfiles may have changed them, so finish by doing this again.
source <("$_flox_activations" set-env-dirs --shell zsh --flox-env "$FLOX_ENV" --env-dirs "${FLOX_ENV_DIRS:-}")
source <("$_flox_activations" fix-paths --shell zsh --env-dirs "$FLOX_ENV_DIRS" --path "$PATH" --manpath "${MANPATH:-}")

# Source user-specified profile scripts if they exist.
# Iterate over $FLOX_ENV_DIRS in reverse order using zsh-specific "(Oa)".
# See: https://www.zsh.org/mla/workers/2003/msg00413.html
  # Our ZDOTDIR startup files source user RC files that may modify FLOX_ENV_DIRS,
  # and then _flox_env_helper may fix it up.
  # If this happens, we want to respect those modifications,
  # so we use FLOX_ENV_DIRS from the environment
IFS=':' read -r -A _flox_env_dirs <<< "$FLOX_ENV_DIRS"
for _flox_env in ${(Oa)_flox_env_dirs[@]}; do
  for i in profile-common profile-zsh hook-script; do
    if [ -e "$_flox_env/activate.d/$i" ]; then
      "$_flox_activate_tracer" "$_flox_env/activate.d/$i" START
      source "$_flox_env/activate.d/$i"
      "$_flox_activate_tracer" "$_flox_env/activate.d/$i" END
    else
      "$_flox_activate_tracer" "$_flox_env/activate.d/$i" NOT FOUND
    fi
  done
done

# Disable command hashing to allow for newly installed flox packages
# to be found immediately. We do this as the very last thing because
# python venv activations can otherwise return nonzero return codes
# when attempting to invoke `hash -r`.
setopt nohashcmds
setopt nohashdirs

# Undo the hashing that plugins like `vcs_info` perform as a side-effect of
# checking `$commands` from `precmd` hooks in interactive shells. We should have
# loaded all user config by now so our hook will come near-to-last. Setting
# `nohashlistall` will prevent them from finding VCS commands altogether.
if [[ -o interactive ]]; then
  # Underscore prefix to prevent it polluting auto-complete.
  function _flox_rehash() {
    hash -r
  }
  autoload -Uz add-zsh-hook
  add-zsh-hook precmd _flox_rehash
fi

# Disable tracing before potentially launching into user shell.
if [ "$_flox_activate_tracelevel" -ge 2 ]; then
  set +x
fi
unset _flox_activate_tracelevel

"$_flox_activate_tracer" "$_activate_d/zsh" END

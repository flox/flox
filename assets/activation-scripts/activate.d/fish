set -gx _gnused "@gnused@"

# Confirm _flox_activate_tracelevel is defined before proceeding.
if not set -q _flox_activate_tracelevel
  echo 'WARNING (fish): _flox_activate_tracelevel not defined .. defaulting to 0' >&2
  set -gx _flox_activate_tracelevel 0
end

# Set verbosity level with a default of 0 if not already set
if test $_flox_activate_tracelevel -ge 2
  set -gx fish_trace 1
end

# The fish --init-command option allows us to source our startup
# file after the normal configuration has been processed, so there
# is no requirement to go back and source the user's own config
# as we do in bash.

# Restore environment variables set in the previous bash initialization.
$_gnused/bin/sed -e 's/^/set -e /' -e 's/$/;/' "$_FLOX_ACTIVATION_STATE_DIR/del.env" | source
$_gnused/bin/sed -e 's/^/set -gx /' -e 's/=/ /' -e 's/$/;/' "$_FLOX_ACTIVATION_STATE_DIR/add.env" | source

# Restore PATH and MANPATH if set in one of the attach scripts.
if set -q _FLOX_RESTORE_PATH
  set -gx PATH $_FLOX_RESTORE_PATH
  set -e _FLOX_RESTORE_PATH
end
if set -q _FLOX_RESTORE_MANPATH
  set -gx MANPATH $_FLOX_RESTORE_MANPATH
  set -e _FLOX_RESTORE_MANPATH
end

# Set the prompt if we're in an interactive shell.
if isatty 1
    source "$_activate_d/set-prompt.fish"
end

# Source user-specified profile scripts if they exist.
for i in profile-common profile-fish
  if test -e "$FLOX_ENV/activate.d/$i"
    source "$FLOX_ENV/activate.d/$i"
  end
end

# fish does not use hashing in the same way bash does, so there's
# nothing to be done here by way of that requirement.

# Disable tracing before potentially launching into user shell.
if test $_flox_activate_tracelevel -ge 2
  set -gx fish_trace 0
end
set -e _flox_activate_tracelevel

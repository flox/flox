setenv _gnused "@gnused@"

# Confirm _flox_activate_tracelevel is defined before proceeding.
if ( ! $?_flox_activate_tracelevel ) then
  sh -c "echo 'WARNING (tcsh): _flox_activate_tracelevel not defined .. defaulting to 0' >&2"
  setenv _flox_activate_tracelevel 0
endif

if ( $_flox_activate_tracelevel >= 2 ) then
  set verbose
endif

# Restore environment variables set in the previous bash initialization.
eval `$_gnused/bin/sed -e 's/^/unsetenv /' -e 's/$/;/' "$_FLOX_ACTIVATION_STATE_DIR/del.env"`
eval `$_gnused/bin/sed -e 's/^/setenv /' -e 's/=/ /' -e 's/$/;/' "$_FLOX_ACTIVATION_STATE_DIR/add.env"`

# Restore PATH and MANPATH if set in one of the attach scripts.
if ( $?_FLOX_RESTORE_PATH ) then
  setenv PATH "$_FLOX_RESTORE_PATH"
  unset _FLOX_RESTORE_PATH
endif
if ( $?_FLOX_RESTORE_MANPATH ) then
  setenv MANPATH "$_FLOX_RESTORE_MANPATH"
  unset _FLOX_RESTORE_MANPATH
endif

# Set the prompt if we're in an interactive shell.
if ( $?tty ) then
  source "$_activate_d/set-prompt.tcsh"
endif

# Source user-specified profile scripts if they exist.
foreach i ( profile-common profile-tcsh )
  if ( -e "$FLOX_ENV/activate.d/$i" ) then
    source "$FLOX_ENV/activate.d/$i"
  endif
end

# Disable command hashing to allow for newly installed flox packages
# to be found immediately.
unhash

# Disable tracing before potentially launching into user shell.
if ( $_flox_activate_tracelevel >= 2 ) then
  unset verbose
endif
unsetenv _flox_activate_tracelevel
